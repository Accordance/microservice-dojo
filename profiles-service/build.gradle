buildscript {
    ext {
        springBootVersion = '1.3.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'docker'

project.ext {
    springBootVersion = '1.3.1.RELEASE'
    springCloudVersion = '1.0.3.RELEASE'
}

jar {
    group = 'msvcdojo'
    baseName = 'profiles-service'
    version = '0.0.2'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
//    maven { url 'http://repo.spring.io/release' }
}

//processResources {
//    filesMatching("**/application.yml") {
//        expand(project.properties)
//    }
//}

bootRun {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
}

test {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion") {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    compile ('org.springframework.boot:spring-boot-starter-jetty') {
        exclude group: 'org.eclipse.jetty.websocket'
    }
    compile("org.springframework.cloud:spring-cloud-config-client:$springCloudVersion")
    compile("org.springframework.cloud:spring-cloud-starter-eureka:$springCloudVersion")
    compile("org.springframework.boot:spring-boot-starter-data-rest:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-actuator:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-data-mongodb:$springBootVersion")
    compile("org.springframework.hateoas:spring-hateoas")
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

task prepDocker(type: Copy, dependsOn: build) {
    from 'docker'
    into "$buildDir/docker"
    include 'Dockerfile.template'
    rename { file -> 'Dockerfile' }
    expand(project: project, jar: jar )
    doFirst {
        copy {
            from jar
            into "$buildDir/docker"
        }
    }
}

task buildDocker(type: Docker, dependsOn: prepDocker) {
    tag = "${jar.group}/${jar.baseName}"
    tagVersion = jar.version
    dockerfile = file("$buildDir/docker/Dockerfile")
}

task wrapper(type: org.gradle.api.tasks.wrapper.Wrapper) {
    gradleVersion = '2.9'
}
