buildscript {
    ext {
        springBootVersion = '1.3.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'

project.ext {
    springBootVersion = '1.3.1.RELEASE'
    springCloudVersion = '1.0.3.RELEASE'
}

jar {
    group = 'msvcdojo'
    baseName = 'profiles-service'
    version = '0.0.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

bootRun {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
}

test {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion") {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    compile ('org.springframework.boot:spring-boot-starter-jetty') {
        exclude group: 'org.eclipse.jetty.websocket'
    }
    compile("org.springframework.cloud:spring-cloud-config-client:$springCloudVersion")
    compile("org.springframework.boot:spring-boot-starter-actuator:$springBootVersion")
// tag::mongo[]
    compile("org.springframework.boot:spring-boot-starter-data-rest:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-data-mongodb:$springBootVersion")
    compile("org.springframework.hateoas:spring-hateoas")
// end::mongo[]
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

task prepDocker(type: Copy, dependsOn: build) {
    from 'docker'
    into "$buildDir/docker"
    include 'Dockerfile.template'
    rename { file -> 'Dockerfile' }
    expand(project: project, jar: jar )
    doFirst {
        copy {
            from jar
            into "$buildDir/docker"
        }
    }
    doLast {
        println "Docker command: docker build -t ${jar.group}/${jar.baseName}:${jar.version} build/docker"
    }
}

task wrapper(type: org.gradle.api.tasks.wrapper.Wrapper) {
    gradleVersion = '2.9'
}
