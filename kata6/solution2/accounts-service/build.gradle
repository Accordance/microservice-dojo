buildscript {
    ext {
        springBootVersion = '1.3.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.flywaydb:flyway-gradle-plugin:3.2.1")
        classpath("mysql:mysql-connector-java:5.1.38")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'org.flywaydb.flyway'

project.ext {
    springBootVersion = '1.3.1.RELEASE'
    springCloudVersion = '1.0.3.RELEASE'
}

jar {
    group = 'msvcdojo'
    baseName = 'accounts-service'
    version = '0.0.2'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

bootRun {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
}

test {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion") {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    compile ('org.springframework.boot:spring-boot-starter-jetty') {
        exclude group: 'org.eclipse.jetty.websocket'
    }
    compile("org.springframework.boot:spring-boot-starter-actuator:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-data-rest:$springBootVersion")
    compile("org.springframework.cloud:spring-cloud-config-client:$springCloudVersion")
    compile("org.springframework.cloud:spring-cloud-starter-eureka:$springCloudVersion")
// tag::feign[]
    compile("org.springframework.cloud:spring-cloud-starter-feign:$springCloudVersion")
// end::feign[]
    compile("org.flywaydb:flyway-core:3.2.1")
    runtime("mysql:mysql-connector-java:5.1.38")
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

flyway {
    url = 'jdbc:mysql://dockerhost:3306/demo'
    user = 'demo_user'
    password = 'demo_pass'
}

task prepDocker(type: Copy, dependsOn: assemble) {
    from 'docker'
    into "$buildDir/docker"
    include 'Dockerfile.template'
    rename { file -> 'Dockerfile' }
    expand(project: project, jar: jar )
    doFirst {
        copy {
            from jar
            into "$buildDir/docker"
        }
    }
}

task wrapper(type: org.gradle.api.tasks.wrapper.Wrapper) {
    gradleVersion = '2.9'
}
