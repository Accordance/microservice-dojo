buildscript {
    ext {
        springBootVersion = '1.3.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'docker'

project.ext {
    springBootVersion = '1.3.1.RELEASE'
    springCloudVersion = '1.0.3.RELEASE'
}

jar {
    baseName = 'hystrix-service'
    version = '0.0.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'http://repo.spring.io/release' }
}

bootRun {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion") {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    compile ('org.springframework.boot:spring-boot-starter-jetty') {
        exclude group: 'org.eclipse.jetty.websocket'
    }
    compile("org.springframework.cloud:spring-cloud-starter-hystrix-dashboard:$springCloudVersion")
    compile("org.springframework.cloud:spring-cloud-starter-eureka:$springCloudVersion")
    compile("org.springframework.cloud:spring-cloud-config-client:$springCloudVersion") {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
    }
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

task prepDocker(type: Copy, dependsOn: assemble) {
    from 'docker'
    into "$buildDir/docker"
    include 'Dockerfile.template'
    rename { file -> 'Dockerfile' }
    expand(project: project, jar: jar )
    doFirst {
        copy {
            from jar
            into "$buildDir/docker"
        }
    }
}

task buildDocker(type: Docker, dependsOn: prepDocker) {
    useApi = false
    tag = "${jar.group}/${jar.baseName}"
    tagVersion = jar.version
    dockerfile = file("$buildDir/docker/Dockerfile")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}
